plugins {
	id "java"
	id "net.minecraftforge.gradle.forge" version "8708bf3e0"
	id "com.github.johnrengelman.shadow" version "6.1.0"
}

version = modVersion
group = modGroup + "." + modId
archivesBaseName = modBaseName

targetCompatibility = sourceCompatibility = JavaVersion.VERSION_1_8
compileJava.options.encoding = "UTF-8"

minecraft {
    version = project.forgeVersion
	mappings = project.mcpVersion
    runDir = "run"
	makeObfSourceJar = false
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io/" }
    maven { url "https://repo.sk1er.club/repository/maven-releases/" }
    maven { url "https://repo.spongepowered.org/maven/" }
}

configurations {
	// Creates an extra configuration that implements `implementation` to be used later as the configuration that shades libraries
    include
    implementation.extendsFrom include
}

dependencies {
    // How to normally add a dependency (If you don"t want it to be added to the jar)
    // implementation "com.example:example:1.0.0"
    // If you would like to include it (have the library inside your jar) instead use
    // include "com.example:example:1.0.0"
	
	include "gg.essential:loader-launchwrapper:1.1.0"
	provided "gg.essential:essential-1.8.9-forge:1291"
}

jar {
	manifest.attributes(
		"TweakClass": "gg.essential.loader.stage0.EssentialSetupTweaker",
		"ModSide": "CLIENT",
		"FMLCorePluginContainsFMLMod": true
	)
	enabled = false
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    filesMatching("mcmod.info") {
        expand "version": project.version, "mcversion": project.minecraft.version, "modId": project.modId, "modBaseName": project.modBaseName
    }
}

task moveResources {
    doLast {
        ant.move (
                file: "${buildDir}/resources/main",
                todir: "${buildDir}/classes/java"
        )
    }
}

tasks.moveResources.dependsOn processResources
tasks.classes.dependsOn moveResources

shadowJar {
    archiveClassifier.set("")

    configurations = [project.configurations.include]
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
}

reobf {
    shadowJar {
        mappingType = "SEARGE"
    }
}

reobfJar.dependsOn tasks.shadowJar